刚才看到标题时，您觉得标题错了吗？那么看完这篇，您应该点赞成（没错嘛）。

曾有人问：可以用中文短语替换中文短语吗？有这样的想法 很好理解，因为热字串的实际操作就是替换，但本质其实是转译。当我们按下一个键时，电流的变化让驱动程序（即硬件接口）得知某个键被按下了，接着发送给操 作系统，这时从扫描码转译为字母或字符（转译规则与键盘布局有关），因此系统会做出某种响应，如是 a 则发送字母 a 到活动窗口，若 PageUp 则发送向上翻页。这是从按下按键到字符上屏或做出控制的粗略过程。系统做出的响应是根据系统中与该键有关的一个表，按优先级顺序一项项进行。而热键则是在 这个表中增加一个转译，以执行我们期望的操作（从这个角度看，输入法也是类似的）。

还记得之前说过的序列键吗？热字串就是序列键。参考简平台的实现。

最后，热键和热字串的双冒号都只是形式， 我们需要了解的是其本质。本篇虽然用热键实现了热字串功能，但实际上只是模拟实现，如何实现并不重要，理解了本质才能融会贯通，正所谓：**应用之妙，存乎一心**。
四、动态正则热字串

RegEx Powered Dynamic Hotstrings
http://www.autohotkey.com/board/topic/15015-regex-powered-dynamic-hotstrings/

Dynamic Hotstrings • AHKScript
http://ahkscript.org/boards/viewtopic.php?f=6&t=3329

又一次让我感慨只有想不到，没有做不到，下面这个是 polyethene 的脚本。

注：由于这个函数使用了全局变量并设置了热键，所以不是纯粹的函数，作为库函数使用可能存在问题。

/*
    Function: hotstrings
        Dynamically adds regular expression hotstrings.

    Parameters:
        c - regular expression hotstring
        a - (optional) text to replace hotstring with or a label to goto, 
            leave blank to remove hotstring definition from triggering an action

    Examples:
> hotstrings("(B|b)tw\s", "%$1%y the way") ; type 'btw' followed by space, tab or return
> hotstrings("i)omg", "oh my god{!}") ; type 'OMG' in any case, upper, lower or mixed
> hotstrings("\bcolou?r", "rgb(128, 255, 0);") ; '\b' prevents matching with anything before the word, e.g. 'multicololoured'

    License:
        - Version 2.59 <http://www.autohotkey.net/~polyethene/#hotstrings>
        - Dedicated to the public domain (CC0 1.0) <http://creativecommons.org/publicdomain/zero/1.0/>
*/

hotstrings(k, a = "")
{
    static z, m = "*~$", s, t, w = 2000, sd, d = "Left,Right,Up,Down,Home,End,RButton,LButton"
    global $
    If z = ; init
    {
        RegRead, sd, HKCU, Control Panel\International, sDecimal
        Loop, 94
        {
            c := Chr(A_Index + 32)
            If A_Index not between 33 and 58
                Hotkey, %m%%c%, __hs
        }
        e = 0,1,2,3,4,5,6,7,8,9,Dot,Div,Mult,Add,Sub,Enter
        Loop, Parse, e, `,
            Hotkey, %m%Numpad%A_LoopField%, __hs
        e = BS,Space,Enter,Return,Tab,%d%
        Loop, Parse, e, `,
            Hotkey, %m%%A_LoopField%, __hs
        z = 1
    }
    If (a == "" and k == "") ; poll
    {
        StringTrimLeft, q, A_ThisHotkey, StrLen(m)
        If q = BS
        {
            If (SubStr(s, 0) != "}")
                StringTrimRight, s, s, 1
        }
        Else If q in %d%
            s =
        Else
        {
            If q = Space
                q := " "
            Else If q = Tab
                q := "`t"
            Else If q in Enter,Return,NumpadEnter
                q := "`n"
            Else If (RegExMatch(q, "Numpad(.+)", n))
            {
                q := n1 == "Div" ? "/" : n1 == "Mult" ? "*" : n1 == "Add" ? "+" : n1 == "Sub" ? "-" : n1 == "Dot" ? sd : ""
                If n1 is digit
                    q = %n1%
            }
            Else If (StrLen(q) != 1)
                q = {%q%}
            Else If (GetKeyState("Shift") ^ GetKeyState("CapsLock", "T"))
                StringUpper, q, q
            s .= q
        }
        Loop, Parse, t, `n ; check
        {
            StringSplit, x, A_LoopField, `r
            If (RegExMatch(s, x1 . "$", $)) ; match
            {
                StringLen, l, $
                StringTrimRight, s, s, l
                SendInput, {BS %l%}
                If (IsLabel(x2))
                    Gosub, %x2%
                Else
                {
                    Transform, x0, Deref, %x2%
                    SendInput, %x0%
                }
            }
        }
        If (StrLen(s) > w)
            StringTrimLeft, s, s, w // 2
    }
    Else ; assert
    {
        StringReplace, k, k, `n, \n, All ; normalize
        StringReplace, k, k, `r, \r, All
        Loop, Parse, t, `n
        {
            l = %A_LoopField%
            If (SubStr(l, 1, InStr(l, "`r") - 1) == k)
                StringReplace, t, t, `n%l%
        }
        If a !=
            t = %t%`n%k%`r%a%
    }
    Return
    __hs: ; event
    hotstrings("", "")
    Return
}  

简平台、小仙并击、HK4WIN、vimd（viatc、vimode）

热键双冒号之前、热字串双冒号之间必须是按键，所以无法中文替换。 
